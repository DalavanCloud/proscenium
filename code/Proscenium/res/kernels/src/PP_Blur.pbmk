<languageVersion: 1.0;>
material kernel PP_Blur
<
	namespace:	"Pixel Bender 3D";
	vendor:		"Adobe";
	version:	1;
>
{
// ================================================================================
// Copyright 2012 Adobe Systems
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
// http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
// ================================================================================

	///////////////////////////////////////////////////////////////////////////
	parameter    float4 size;  		  // xy = 1/wh, zw=wh
	parameter    float4 sizeHalf4th;  // xy = 1/wh/2,  zw = 1/wh/4

	input        image4 sourceImage;
	interpolated float4 voutUV;

	parameter    float4 uvScale;   
	parameter    float4 uvOffset;
	input vertex float2 viewportUV  < id:"texcoord"; >;
	void evaluateVertex()	{ voutUV = viewportUV.xyxy * uvScale + uvOffset; }
	///////////////////////////////////////////////////////////////////////////

	// 3x3 kernel:	
	//  [ 1  2  1 ]       1
	//  [ 2  4  2 ]  *  ----
	//  [ 1  2  1 ]      16
	// this is equivalent to sampling at the centers of quadrants and taking average.
	
	output float4 outputColor;
	void evaluateFragment()
	{
		float4 uvd = sizeHalf4th.xyxy;
		float4 color0 = sample(sourceImage, float2(voutUV.x - uvd.x, voutUV.y - uvd.y), PB3D_LINEAR | PB3D_MIPDISABLE ); 
		float4 color1 = sample(sourceImage, float2(voutUV.x + uvd.x, voutUV.y - uvd.y), PB3D_LINEAR | PB3D_MIPDISABLE ); 
		float4 color2 = sample(sourceImage, float2(voutUV.x - uvd.x, voutUV.y + uvd.y), PB3D_LINEAR | PB3D_MIPDISABLE ); 
		float4 color3 = sample(sourceImage, float2(voutUV.x + uvd.x, voutUV.y + uvd.y), PB3D_LINEAR | PB3D_MIPDISABLE ); 

		outputColor  = (color0 + color1 + color2 + color3) * 0.25;
	}
}
